#define MODNAME "SAMPLE_DRIVER"

module sample_driver_mod

  !-----------------------------------------------------------------------------
  ! Code that specializes generic NUOPC_Driver
  !-----------------------------------------------------------------------------

  use ESMF
  use NUOPC
  use NUOPC_Driver, &
    driver_routine_SS             => SetServices, &
    driver_label_SetModelServices => label_SetModelServices

#ifdef COMP1_MOD
      use COMP1_MOD, only: COMP1_SS => SetServices
#endif
#ifdef COMP2_MOD
      use COMP2_MOD, only: COMP2_SS => SetServices
#endif
#ifdef COMP3_MOD
      use COMP3_MOD, only: COMP3_SS => SetServices
#endif
#ifdef COMP4_MOD
      use COMP4_MOD, only: COMP4_SS => SetServices
#endif
#ifdef COMP5_MOD
      use COMP5_MOD, only: COMP5_SS => SetServices
#endif
  
  implicit none
  
  private
  
  public SetServices

  CHARACTER(LEN=*), PARAMETER :: label_InternalState = 'InternalState'
  CHARACTER(LEN=*), PARAMETER :: defaultConfigFile   = 'sample.rc'
  CHARACTER(LEN=16), DIMENSION(10), PARAMETER :: CUSTOMFIELDLIST = (/ &
    "dummy_field_1 ", "dummy_field_2 ", &
    "dummy_field_3 ", "dummy_field_4 ", &
    "dummy_field_5 ", "dummy_field_6 ", &
    "dummy_field_7 ", "dummy_field_8 ", &
    "dummy_field_9 ", "dummy_field_10" /)
  CHARACTER(LEN=10), DIMENSION(10), PARAMETER :: CUSTOMUNITSLIST = (/ &
    "Pa   ",         "kg   ", &
    "W m-2",         "m    ", &
    "kg   ",         "m s-1", &
    "kg   ",         "W m-2", &
    "m    ",         "K    "  /)

  type model_internalstate_type
    logical           :: defaultConfig    = .FALSE.
    integer           :: verbosity        = 1
    integer           :: timeStepSeconds  = 3600
    integer           :: stepCount        = 24
  end type

  type model_internalstate_wrapper
    type(model_internalstate_type), pointer :: wrap
  end type
  
  !-----------------------------------------------------------------------------
  contains
  !-----------------------------------------------------------------------------

#undef METHOD
#define METHOD "SetServices"

  subroutine SetServices(driver, rc)
    type(ESMF_GridComp)  :: driver
    integer, intent(out) :: rc

#ifdef DEBUG
    call ESMF_LogWrite(MODNAME//": entered "//METHOD, ESMF_LOGMSG_INFO)
#endif

    rc = ESMF_SUCCESS
    
    ! NUOPC_Driver registers the generic methods
    call NUOPC_CompDerive(driver, driver_routine_SS, rc=rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
      line=__LINE__, file=__FILE__)) return ! bail out
      
    ! attach specializing method(s)
    call NUOPC_CompSpecialize(driver, specLabel=driver_label_SetModelServices, &
      specRoutine=SetModelServices, rc=rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
      line=__LINE__, file=__FILE__)) return ! bail out

#ifdef DEBUG
    call ESMF_LogWrite(MODNAME//": leaving "//METHOD, ESMF_LOGMSG_INFO)
#endif

  end subroutine

  !-----------------------------------------------------------------------------

#undef METHOD
#define METHOD "SetModelServices"

  subroutine SetModelServices(driver, rc)
    type(ESMF_GridComp)  :: driver
    integer, intent(out) :: rc
    
    ! local variables
    character(ESMF_MAXSTR)            :: dname
    integer                           :: stat
    type(model_internalstate_wrapper) :: is
    logical                           :: configIsPresent
    type(ESMF_Config)                 :: config
    character(ESMF_MAXSTR)            :: tmpStr
    type(NUOPC_FreeFormat)            :: attrFF
    type(ESMF_Time)                   :: startTime
    type(ESMF_Time)                   :: stopTime
    type(ESMF_TimeInterval)           :: timeStep
    type(ESMF_Clock)                  :: internalClock
    integer                           :: compListSize
    character(len=32), allocatable    :: compList(:)
    character(len=32)                 :: compMod
    character(len=32)                 :: compName
    integer                           :: i
    type(ESMF_GridComp)               :: child

#ifdef DEBUG
    call ESMF_LogWrite(MODNAME//": entered "//METHOD, ESMF_LOGMSG_INFO)
#endif

    rc = ESMF_SUCCESS

    ! query the Driver for info
    call ESMF_GridCompGet(driver, name=dname, rc=rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
      line=__LINE__, file=__FILE__)) return ! bail out

    ! allocate memory for this internal state and set it in the
    ! component
    allocate(is%wrap, stat=stat)
    if (ESMF_LogFoundAllocError(statusToCheck=stat, &
      msg='Allocation of internal state memory failed.', &
      method=METHOD, file=__FILE__, rcToReturn=rc)) return ! bail out
    call ESMF_UserCompSetInternalState(driver, label_InternalState, is, rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
      line=__LINE__, file=__FILE__)) return ! bail out

    ! log component modules built into executable
    if (is%wrap%verbosity .gt. 0) then
      write (tmpStr, "(A,A)") trim(dname)//': ', &
        'Component Modules in Executable'
      call ESMF_LogWrite(trim(tmpStr),ESMF_LOGMSG_INFO)
#ifdef COMP1_MOD
      write (tmpStr, "(A,A20)") trim(dname)//': ', COMP1_STR
      call ESMF_LogWrite(trim(tmpStr),ESMF_LOGMSG_INFO)
#endif
#ifdef COMP2_MOD
      write (tmpStr, "(A,A20)") trim(dname)//': ', COMP2_STR
      call ESMF_LogWrite(trim(tmpStr),ESMF_LOGMSG_INFO)
#endif
#ifdef COMP3_MOD
      write (tmpStr, "(A,A20)") trim(dname)//': ', COMP3_STR
      call ESMF_LogWrite(trim(tmpStr),ESMF_LOGMSG_INFO)

#endif
#ifdef COMP4_MOD
      write (tmpStr, "(A,A20)") trim(dname)//': ', COMP4_STR
      call ESMF_LogWrite(trim(tmpStr),ESMF_LOGMSG_INFO)

#endif
#ifdef COMP5_MOD
      write (tmpStr, "(A,A20)") trim(dname)//': ', COMP5_STR
      call ESMF_LogWrite(trim(tmpStr),ESMF_LOGMSG_INFO)
#endif
      endif

    ! check driver for config
    call ESMF_GridCompGet(driver, configIsPresent=configIsPresent, rc=rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
      line=__LINE__, file=__FILE__)) return ! bail out

    if (configIsPresent) then

      is%wrap%defaultConfig = .FALSE.

      call ESMF_GridCompGet(driver, config=config, rc=rc)
      if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
        line=__LINE__, file=__FILE__)) return ! bail out  else

    else

      is%wrap%defaultConfig = .TRUE.

      ! create config file
      config = ESMF_ConfigCreate(rc=rc)
      if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
        line=__LINE__, file=__FILE__)) return ! bail out

      ! load the default config file
      call ESMF_ConfigLoadFile(config, trim(defaultConfigFile), rc=rc)
      if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
        line=__LINE__, file=__FILE__)) return ! bail out

      ! set the config file
      call ESMF_GridCompSet(driver, config=config, rc=rc)
      if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
        line=__LINE__, file=__FILE__)) return ! bail out

    endif

    ! read and ingest free format driver attributes
    attrFF = NUOPC_FreeFormatCreate(config, &
      label=trim(dname)//"_attributes::", relaxedflag=.true., rc=rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
      line=__LINE__, file=__FILE__)) return ! bail out
    call NUOPC_CompAttributeIngest(driver, attrFF, addFlag=.true., rc=rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
      line=__LINE__, file=__FILE__)) return ! bail out
    call NUOPC_FreeFormatDestroy(attrFF, rc=rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
      line=__LINE__, file=__FILE__)) return ! bail out

    ! determine the component list
    compListSize = ESMF_ConfigGetLen(config, &
      label=trim(dname)//"_component_list:", rc=rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
      line=__LINE__, file=__FILE__)) return ! bail out
    allocate(compList(compListSize), stat=stat)
    if (ESMF_LogFoundAllocError(statusToCheck=stat, &
      msg="Allocation of component list memory failed.", &
      method=METHOD, file=__FILE__, rcToReturn=rc)) return ! bail out
    call ESMF_ConfigGetAttribute(config, valueList=compList, &
      label=trim(dname)//"_component_list:", count=compListSize, rc=rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
       line=__LINE__, file=__FILE__)) return ! bail out

    ! read and ingest free format driver attributes
    attrFF = NUOPC_FreeFormatCreate(config, &
      label=trim(dname)//"_attributes::", relaxedflag=.true., rc=rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
      line=__LINE__, file=__FILE__)) return ! bail out
    call NUOPC_CompAttributeIngest(driver, attrFF, addFlag=.true., rc=rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
      line=__LINE__, file=__FILE__)) return ! bail out
    call NUOPC_FreeFormatDestroy(attrFF, rc=rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
      line=__LINE__, file=__FILE__)) return ! bail out

    ! store attributes in internal state
    call ESMF_AttributeGet(driver, name="verbosity", value=tmpStr, &
      defaultValue="default", convention="NUOPC", purpose="Instance", rc=rc)
    is%wrap%verbosity = ESMF_UtilString2Int(tmpStr, &
      specialStringList=(/"default","none   ","max    "/), &
      specialValueList=(/is%wrap%verbosity,0,255/), rc=rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
      line=__LINE__, file=__FILE__)) return ! bail out
    call ESMF_AttributeGet(driver, name="time_step_seconds", value=tmpStr, &
      defaultValue="default", convention="NUOPC", purpose="Instance", rc=rc)
    is%wrap%timeStepSeconds = ESMF_UtilString2Int(tmpStr, &
      specialStringList=(/"default"/), &
      specialValueList=(/is%wrap%timeStepSeconds/), rc=rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
      line=__LINE__, file=__FILE__)) return ! bail out
    call ESMF_AttributeGet(driver, name="step_count", value=tmpStr, &
      defaultValue="default", convention="NUOPC", purpose="Instance", rc=rc)
    is%wrap%stepCount = ESMF_UtilString2Int(tmpStr, &
      specialStringList=(/"default"/), &
      specialValueList=(/is%wrap%stepCount/), rc=rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
      line=__LINE__, file=__FILE__)) return ! bail out

    ! log driver attributes
    if (is%wrap%verbosity .gt. 0) then

      write (tmpStr, "(A,A)") trim(dname)//': ', &
        'Driver Attributes'
      call ESMF_LogWrite(trim(tmpStr),ESMF_LOGMSG_INFO)
      write (tmpStr, "(A,A20,I0)") trim(dname)//': ', &
        'Verbosity: ',is%wrap%verbosity
      call ESMF_LogWrite(trim(tmpStr),ESMF_LOGMSG_INFO)
      write (tmpStr, "(A,A20,I0)") trim(dname)//': ', &
        'Time Step: ',is%wrap%timeStepSeconds
      call ESMF_LogWrite(trim(tmpStr),ESMF_LOGMSG_INFO)
      write (tmpStr, "(A,A20,I0)") trim(dname)//': ', &
        'Step Count: ',is%wrap%stepCount
      call ESMF_LogWrite(trim(tmpStr),ESMF_LOGMSG_INFO)

    endif

    ! determine information for each component and add to the driver
    do i=1, compListSize

      call ESMF_ConfigGetAttribute(config, compMod, &
        label=trim(compList(i))//"_mod:", &
        default=trim(compList(i)), rc=rc)
      if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
        line=__LINE__, file=__FILE__)) return ! bail out

      call ESMF_ConfigGetAttribute(config, compName, &
        label=trim(compList(i))//"_name:", &
        default=trim(compList(i)), rc=rc)
      if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
        line=__LINE__, file=__FILE__)) return ! bail out

      select case (trim(compMod))
#ifdef COMP1_MOD
        case (COMP1_STR)
          ! SetServices for component 1
          call NUOPC_DriverAddComp(driver, trim(compName), COMP1_SS, &
            comp=child, rc=rc)
          if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
            line=__LINE__, file=__FILE__)) return ! bail out
#endif
#ifdef COMP2_MOD
        case (COMP2_STR)
          ! SetServices for component 2
          call NUOPC_DriverAddComp(driver, trim(compName), COMP2_SS, &
            comp=child, rc=rc)
          if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
            line=__LINE__, file=__FILE__)) return ! bail out
#endif
#ifdef COMP3_MOD
        case (COMP3_STR)
          ! SetServices for component 3
          call NUOPC_DriverAddComp(driver, trim(compName), COMP3_SS, &
            comp=child, rc=rc)
          if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
            line=__LINE__, file=__FILE__)) return ! bail out
#endif
#ifdef COMP4_MOD
        case (COMP4_STR)
          ! SetServices for component 4
          call NUOPC_DriverAddComp(driver, trim(compName), COMP4_SS, &
            comp=child, rc=rc)
          if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
            line=__LINE__, file=__FILE__)) return ! bail out
#endif
#ifdef COMP5_MOD
        case (COMP5_STR)
          ! SetServices for component 5
          call NUOPC_DriverAddComp(driver, trim(compName), COMP5_SS, &
            comp=child, rc=rc)
          if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
            line=__LINE__, file=__FILE__)) return ! bail out
#endif
        case default
          call ESMF_LogSetError(rcToCheck=ESMF_RC_NOT_FOUND, &
            msg=trim(compMod)//" was requested "// &
              "but is not available in the executable!", &
            method=METHOD, file=__FILE__, rcToReturn=rc)
          return ! bail out
      end select

      ! read and ingest free format component attributes
      attrFF = NUOPC_FreeFormatCreate(config, &
        label=trim(compList(i))//"_attributes::", relaxedflag=.true., rc=rc)
      if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
        line=__LINE__, file=__FILE__)) return ! bail out
      call NUOPC_CompAttributeIngest(child, attrFF, addFlag=.true., rc=rc)
      if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
        line=__LINE__, file=__FILE__)) return ! bail out
      call NUOPC_FreeFormatDestroy(attrFF, rc=rc)
      if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
        line=__LINE__, file=__FILE__)) return ! bail out

    enddo

    if (is%wrap%verbosity .gt. 0) then

      write (tmpStr, "(A,A)") trim(dname)//': ', &
        'Active Components'
      call ESMF_LogWrite(trim(tmpStr),ESMF_LOGMSG_INFO)

      do i=1, compListSize

        call ESMF_ConfigGetAttribute(config, compName, &
          label=trim(compList(i))//"_name:", &
          default=trim(compList(i)), rc=rc)
        if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
          line=__LINE__, file=__FILE__)) return ! bail out
        write (tmpStr, "(A,A20)") trim(dname)//': ', &
          trim(compName)
        call ESMF_LogWrite(trim(tmpStr),ESMF_LOGMSG_INFO)

     enddo

    endif

    deallocate(compList, stat=stat)
    if (ESMF_LogFoundDeallocError(statusToCheck=stat, &
      msg="Deallocation of component list memory failed.", &
      line=__LINE__, file=__FILE__)) return  ! bail out

    ! set the driver clock
    call ESMF_TimeSet(startTime, s = 0, rc=rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
      line=__LINE__, file=__FILE__)) &
      call ESMF_Finalize(endflag=ESMF_END_ABORT)

    call ESMF_TimeSet(stopTime, &
      s=(is%wrap%timeStepSeconds * is%wrap%stepCount), rc=rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
      line=__LINE__, file=__FILE__)) &
      call ESMF_Finalize(endflag=ESMF_END_ABORT)

    call ESMF_TimeIntervalSet(timeStep, s=is%wrap%timeStepSeconds, rc=rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
      line=__LINE__, file=__FILE__)) &
      call ESMF_Finalize(endflag=ESMF_END_ABORT)

    internalClock = ESMF_ClockCreate(name=trim(dname)//"_clock", &
      timeStep=timeStep, startTime=startTime, stopTime=stopTime, rc=rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
      line=__LINE__, file=__FILE__)) &
      call ESMF_Finalize(endflag=ESMF_END_ABORT)

    call ESMF_GridCompSet(driver, clock=internalClock, rc=rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
      line=__LINE__, file=__FILE__)) return ! bail out

    call CustomFieldSetup(driver, rc=rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
      line=__LINE__, file=__FILE__)) return ! bail out

#ifdef DEBUG
    call ESMF_LogWrite(MODNAME//": leaving "//METHOD, ESMF_LOGMSG_INFO)
#endif

  end subroutine

  !-----------------------------------------------------------------------------

#undef METHOD
#define METHOD "CustomFieldSetup"

  subroutine CustomFieldSetup(driver, rc)
    type(ESMF_GridComp)  :: driver
    integer, intent(out) :: rc

    ! local variables
    character(ESMF_MAXSTR)            :: dname
    type(model_internalstate_wrapper) :: is
    integer                           :: i
    character(ESMF_MAXSTR)            :: tmpStr

#ifdef DEBUG
    call ESMF_LogWrite(MODNAME//": entered "//METHOD, ESMF_LOGMSG_INFO)
#endif

    rc = ESMF_SUCCESS

    ! query the Driver for info
    call ESMF_GridCompGet(driver, name=dname, rc=rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
      line=__LINE__, file=__FILE__)) return ! bail out

    ! query component for its internal State
    nullify(is%wrap)
    call ESMF_UserCompGetInternalState(driver, label_InternalState, is, rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
      line=__LINE__, file=__FILE__)) return ! bail out

      if (is%wrap%verbosity .gt. 0) then

        write (tmpStr, "(A,A)") trim(dname)//': ', &
          'Custom Dictionary Fields'
        call ESMF_LogWrite(trim(tmpStr),ESMF_LOGMSG_INFO)

      endif


    do i=1, size(CUSTOMFIELDLIST)

      if (.NOT.NUOPC_FieldDictionaryHasEntry(trim(CUSTOMFIELDLIST(i)))) then
        call NUOPC_FieldDictionaryAddEntry( &
          standardName=trim(CUSTOMFIELDLIST(i)), &
          canonicalUnits=trim(CUSTOMUNITSLIST(i)), rc=rc)
        if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
          line=__LINE__, file=__FILE__)) return ! bail out
        if (is%wrap%verbosity .ge. 1) then

          write (tmpStr, "(A,A20,A10)") trim(dname)//': ', &
            trim(CUSTOMFIELDLIST(i)),trim(CUSTOMUNITSLIST(i))
          call ESMF_LogWrite(trim(tmpStr),ESMF_LOGMSG_INFO)

        endif
      endif

    enddo

#ifdef DEBUG
    call ESMF_LogWrite(MODNAME//": leaving "//METHOD, ESMF_LOGMSG_INFO)
#endif

  end subroutine

end module
